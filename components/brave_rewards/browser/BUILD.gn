import("//brave/components/brave_rewards/browser/buildflags/buildflags.gni")
import("//extensions/buildflags/buildflags.gni")

source_set("browser") {
  public_deps = [
    "buildflags",
  ]

  sources = [
    "balance_report.cc",
    "balance_report.h",
    "rewards_notification_service.cc",
    "rewards_notification_service.h",
    "rewards_notification_service_impl.cc",
    "rewards_notification_service_impl.h",
    "rewards_notification_service_observer.h",
    "content_site.cc",
    "content_site.h",
    "rewards_service.cc",
    "rewards_service.h",
    "rewards_service_factory.cc",
    "rewards_service_factory.h",
    "rewards_service_observer.h",
    "wallet_properties.cc",
    "wallet_properties.h",
    "grant.cc",
    "grant.h",
    "publisher_banner.cc",
    "publisher_banner.h",
    "contribution_info.cc",
    "contribution_info.h",
    "recurring_donation.cc",
    "recurring_donation.h",
  ]

  if (enable_extensions) {
    sources += [
      "extension_rewards_notification_service_observer.cc",
      "extension_rewards_notification_service_observer.h",
      "extension_rewards_service_observer.cc",
      "extension_rewards_service_observer.h",
    ]
  }

  deps = [
    "//base",
    "//components/keyed_service/content",
    "//components/keyed_service/core",
    "//components/sessions",
    # for profile.h
    "//components/domain_reliability",
    "//content/public/browser",
    "//content/public/common",
    "//services/network/public/mojom",
    "//third_party/leveldatabase",
  ]

  if (brave_rewards_enabled) {
    sources += [
      "net/network_delegate_helper.cc",
      "net/network_delegate_helper.h",
      "rewards_service_impl.cc",
      "rewards_service_impl.h",
      "rewards_helper.cc",
      "rewards_helper.h",
      "publisher_info_backend.cc",
      "publisher_info_backend.h",
      "publisher_info_database.cc",
      "publisher_info_database.h",
      "rewards_fetcher_service_observer.cc",
      "rewards_fetcher_service_observer.h",
    ]

    deps += [
      "//brave/vendor/bat-native-ledger",
      "//net",
      "//url",
      # for one shot event
      "//extensions/common",
    ]
  }
}
